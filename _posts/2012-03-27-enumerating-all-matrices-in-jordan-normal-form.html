---
layout: post
title: Enumerating all matrices in Jordan normal form
type: post
categories:
- mathematics
tags:
- programming
- algebraic geometry
author: Pieter Belmans
---

<h2>Introduction</h2>
<p>For a course in noncommutative geometry / representation theory we discussed two examples of classifying orbits of matrices, more specifically Examples 1.1 and 1.2 in <a href="http://win.ua.ac.be/~lebruyn/b2hd-LeBruyn2007a.html">Noncommutative Geometry and Cayley-smooth Orders</a>. I won't discuss the details of the mathematics behind them, the reference should suffice.
<p>I was intrigued by the computational aspects of the problem (the computer scientist in me pops up from time to time), and I recently gained access to a small supercomputer with a <a href="http://magma.maths.usyd.edu.au/magma/">Magma installation</a>. Not knowing the complete classification, which is given in Section 2.8 of <a href="http://win.ua.ac.be/~lebruyn/b2hd-LeBruyn2007a.html">Noncommutative Geometry and Cayley-smooth Orders</a> under the monicker of the Gerstenhaber-Hesselink theorem (I always read the Gerstenlink-Haber theorem for some reason), I have implemented a general algorithm capable of giving you the details for all matrix rings if you are patient enough.
<p>There are three parts involved in the classification:
<ol>
  <li>studying the structure of the variety of singularities
  <li>enumerating all representatives of orbits, which are determined by Jordan normal forms
  <li>determining the dimensions of the orbits
</ol>
<p>The first part is computationally absolutely horrendous, but that could be my fault. I will discuss it if I feel compelled, but at the moment I am not happy with it. Degrees two and three are worked out in detail in the book, degree four is reasonably fast in my implementation while degree five already takes almost an hour... The results are not that uninteresting, so maybe I'll write about them, maybe not.

<h2>Jordan normal form enumeration</h2>
<p>The second part is what this post is about. Recall that any (complex) matrix can be conjugated with an invertible matrix to a block diagonal matrix, whose blocks are matrices with a single eigenvalue of the original on the diagonal, ones on the superdiagonal and zeroes everywhere else. For details, check Wikipedia's <a href="http://en.wikipedia.org/wiki/Jordan_normal_form#Complex_matrices">Jordan normal form/Complex matrices</a>. When trying to reproduce the diagram of Example 1.2 containing the orbit dimensions we obviously need to enumerate all possible Jordan normal forms. This is actually quite easy:
<ol>
  <li>consider all partitions of $n$, where the numbers are given in decreasing order giving all possible configurations of the eigenvalues (these correspond to the columns in the diagram)
  <li>fill in all legal superdiagonals, only considering <em>decreasing numbers of ones</em> for eigenvalues with the same multiplicity
</ol>

<p>The eigenvalues of the representatives are taken to be unknowns in a function field, for maximal flexibility. So the following code

{% highlight magma %}
n := 5;
field := FunctionField(RationalField(), n);
variables := ["x", "y", "z", "u", "v", "w"];
AssignNames(~field, variables[1 .. n]);
for partition in Partitions(n) do
  representative := DiagonalFromPartition(field, partition);
  "Enumerating the orbits with representative", representative;
  for M in EnumerateSuperdiagonals(representative, partition) do
    M, " ";
  end for;
end for;
{% endhighlight %}

does some initialization and will print all representatives, ordered by the structure of the eigenvalues. The code for <code>DiagonalFromPartition</code> is

{% highlight magma %}
DiagonalFromPartition := function(ring, partition)
  M := Matrix(ring, Weight(partition), Weight(partition), []);
  i := 1; j := 1;
  for multiplicity in partition do
    for k in [1 .. multiplicity] do M[j, j] := ring.i; j := j + 1; end for;
    i := i + 1;
  end for;

  return M;
end function;
{% endhighlight %}

<p>while <code>EnumerateSuperDiagonals</code> is

{% highlight magma %}
EnumerateSuperdiagonals := function(matrix, partition)
  matrices := {};

  configurations := CartesianProduct([[0 .. multiplicity - 1] : multiplicity in partition]);
  for configuration in configurations do
    if IsNondecreasing(partition, configuration) then
      matrices := matrices join {ConstructJordanNormalForm(matrix, partition, configuration)};
    end if;
  end for;

  return matrices;
end function;
{% endhighlight %}

<p>The filter <code>IsNondecreasing</code> is implemented by

{% highlight magma %}
IsNondecreasing := function(partition, configuration)
  maximum := Infinity();
  length := partition[1];

  for i in [1 .. #partition] do
    if partition[i] lt length then
      maximum := Infinity();
      length := partition[i];
    elif configuration[i] gt maximum then
      return false;
    else
      maximum := configuration[i];
    end if;
  end for;

  return true;
end function;
{% endhighlight %}

<p>while the actual construction as implemented in <code>ConstructJordanNormalForm</code> is given by

{% highlight magma %}
ConstructJordanNormalForm := function(matrix, partition, configuration)
  position := 1;
  for i in [1 .. #configuration] do
    for j in [0 .. configuration[i] - 1] do
      matrix[position + j, position + j + 1] := 1;
    end for;
    position := position + (partition[i]);
  end for;

  return matrix;
end function;
{% endhighlight %}

<p>The output for this code (where $n=5$) is given in <a href="http://pastebin.com/bBxbNtsQ">this pastebin</a>.
<h2>The calculation of the dimensions</h2>
<p>The third part is a bit of (symbolic) linear algebra, where you have to determine the dimension of the orbit by calculating the dimension of the stabilizer group, which is a Zariski dense subset of the centralizer of a matrix, hence suffices for giving the correct dimension. Mathematical details can be found in the text, the actual implementation is given by

{% highlight magma %}
OrbitDimension := function(matrix)
  field := BaseRing(matrix);
  n := Rank(field);
  ring := PolynomialRing(field, n^2);

  unknowns := Matrix(ring, n, n, [Name(ring, i) : i in [1 .. n^2]]);
  system := Matrix(ring, n^2, n^2, []);

  for i in [1 .. n] do
    for j in [1 .. n] do
      for k in [1 .. n^2] do
        system[(i-1)*n + j, k] := Coefficient((matrix*unknowns)[i, j], k, 1) - Coefficient((unknowns*matrix)[i, j], k, 1);
      end for;
    end for;
  end for;

  return n^2 - Rank(Kernel(system));
end function;
{% endhighlight %}

The algorithm is nothing but initializing a big system of linear equations, expressing the commutator relation $AM=MA$ where $M$ is (not necessarily) a matrix in Jordan normal form and $A$ is a matrix where every element corresponds to an unknown. By equating $AM=MA$ positionwise and obtaining a linear system of $n^2$ equations over the function field where our eigenvalues live, we can compute the rank of the kernel, which describes the dimension of the centralizing matrices.
<p>If we slightly change the iteration of the first code example, calling <code>OrbitDimension</code> for each matrix, the output will be as given in <a href="http://pastebin.com/cTqND1cY">this pastebin</a> in case of $n=5$.
<p>What I have done is not the most relevant contribution to the world of mathematics, but in case you ever feel the urge to enumerate Jordan normal forms, I hope this will help you.
